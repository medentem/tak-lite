plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'       // use canonical id
    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.dagger.hilt.android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.google.protobuf'
    id 'kotlin-parcelize'
}

// Load MAPTILER_API_KEY from local.properties after plugins block
import java.util.Properties

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}
def maptileApiKey = localProperties.getProperty("MAPTILER_API_KEY", "")
def owmApiKey = localProperties.getProperty("OPENWEATHERMAP_API_KEY", "")
def owfApiKey = localProperties.getProperty("OPENWEATHERFORECAST_API_KEY", "")

android {
    namespace "com.tak.lite"
    compileSdk 35

    defaultConfig {
        applicationId "com.tak.lite"
        minSdk 26       // <â€” modern key
        targetSdk 35
        versionCode 97
        versionName "1.2.1-beta.67"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [
                MAPS_API_KEY: project.findProperty('MAPS_API_KEY') ?: ''
        ]
        buildConfigField "String", "MAPTILER_API_KEY", "\"${maptileApiKey}\""
        buildConfigField "String", "OPENWEATHERMAP_API_KEY", "\"${owmApiKey}\""
        buildConfigField "String", "OPENWEATHERFORECAST_API_KEY", "\"${owfApiKey}\""
        
        // Localization support
        resConfigs "en", "es", "fr", "it", "pt", "de", "rm"
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    
    kotlinOptions {
        jvmTarget = '17'
    }
    
    buildFeatures {
        viewBinding true
        buildConfig true
        aidl true
    }

    kapt {
        correctErrorTypes true
        useBuildCache true
        arguments {
            arg("dagger.formatGeneratedSource", "enabled")
        }
    }

    sourceSets {
        main {
        }
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.3'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    // option "lite" // (commented out in Meshtastic)
                }
                kotlin {
                }
            }
        }
    }
}

dependencies {
    // Core Android dependencies
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    
    // Location services
    implementation 'com.google.android.gms:play-services-location:21.1.0'
    
    // Maps
    implementation 'com.google.android.gms:play-services-maps:18.2.0'
    
    // Room for local storage
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    
    // Coroutines for async operations
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.7.3'
    
    // Serialization
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0'
    
    // Lifecycle components
    def lifecycle_version = "2.7.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    
    // Hilt for dependency injection
    implementation 'com.google.dagger:hilt-android:2.51.1'
    kapt 'com.google.dagger:hilt-android-compiler:2.51.1'
    implementation 'androidx.hilt:hilt-navigation:1.1.0'
    implementation 'androidx.hilt:hilt-work:1.1.0'
    kapt 'androidx.hilt:hilt-compiler:1.1.0'
    implementation 'androidx.work:work-runtime-ktx:2.9.0'
    
    // WebRTC
    implementation 'com.dafruits:webrtc:123.0.0'
    
    // Testing dependencies
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // MapLibre GL Android SDK
    implementation 'org.maplibre.gl:android-sdk:11.12.2'

    implementation 'com.google.protobuf:protobuf-java:3.25.3'
    implementation("com.google.protobuf:protobuf-kotlin:3.25.3")

    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'
    
    // Google Play Billing Library
    implementation 'com.android.billingclient:billing-ktx:7.0.0'

    // Smart replies with local model
    implementation 'com.google.mlkit:smart-reply:17.0.4'
    
    // QR Code generation
    implementation 'com.google.zxing:core:3.5.2'
}